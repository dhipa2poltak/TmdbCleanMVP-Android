plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
  id 'kotlin-kapt'
}

android {
  /*
  signingConfigs {
    release {
      storeFile file('/Users/user/Documents/mykeystores/tmdb.keystore')
      storePassword 'tmdb1234'
      keyAlias 'tmdb'
      keyPassword 'tmdb1234'
    }
  }
  */

  namespace = "com.dpfht.tmdbcleanmvp"
  compileSdk ConfigData.compileSdkVersion

  defaultConfig {
    applicationId "com.dpfht.tmdbcleanmvp"

    minSdk ConfigData.minSdkVersion
    targetSdk ConfigData.targetSdkVersion
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

      //signingConfig signingConfigs.release

      manifestPlaceholders.appNameSuffix = ""
    }
    debug {
      applicationIdSuffix ".debug"
      versionNameSuffix "-debug"
      manifestPlaceholders.appNameSuffix = " (debug)"
    }
  }
  buildFeatures {
    buildConfig true
    viewBinding true
    dataBinding true
  }
  compileOptions {
    sourceCompatibility ConfigData.sourceCompatibilityVersion
    targetCompatibility ConfigData.targetCompatibilityVersion
  }
  kotlinOptions {
    jvmTarget = ConfigData.jvmTargetVersion
  }

  flavorDimensions "default"

  productFlavors {
    staging {
      applicationId "com.dpfht.tmdbcleanmvp.staging"

      manifestPlaceholders.appName = "TMDB Clean MVP-Staging"
      buildConfigField 'String', 'BASE_URL', STAGING_BASE_URL
      buildConfigField 'String', 'IMAGE_URL_BASE_PATH', STAGING_IMAGE_URL_BASE_PATH
    }
    verify {
      applicationId "com.dpfht.tmdbcleanmvp.verify"

      manifestPlaceholders.appName = "TMDB Clean MVP-Verify"
      buildConfigField 'String', 'BASE_URL', VERIFY_BASE_URL
      buildConfigField 'String', 'IMAGE_URL_BASE_PATH', VERIFY_IMAGE_URL_BASE_PATH
    }
    prod {
      applicationId "com.dpfht.tmdbcleanmvp"

      manifestPlaceholders.appName = "TMDB Clean MVP"
      buildConfigField 'String', 'BASE_URL', PROD_BASE_URL
      buildConfigField 'String', 'IMAGE_URL_BASE_PATH', PROD_IMAGE_URL_BASE_PATH
    }
  }
}

dependencies {

  implementation fileTree(dir: "libs", include: ["*.jar"])
  implementation project(':framework')
  implementation project(":navigation")

  implementation 'androidx.core:core-ktx:1.9.0'
  implementation 'androidx.appcompat:appcompat:1.6.1'
  implementation 'com.google.android.material:material:1.8.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
  implementation 'androidx.legacy:legacy-support-v4:1.0.0'
  testImplementation 'junit:junit:'
  androidTestImplementation 'androidx.test.ext:junit:1.1.5'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

  implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
  implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'

  testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.2'
  testImplementation 'org.mockito.kotlin:mockito-kotlin:3.2.0'
  testImplementation 'androidx.arch.core:core-testing:2.2.0'
}
